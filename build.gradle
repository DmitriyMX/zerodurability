//file:noinspection GroovyAssignabilityCheck
//file:noinspection SpellCheckingInspection
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath('com.squareup:javapoet:1.13.0')
    }
}

import com.squareup.javapoet.FieldSpec
import com.squareup.javapoet.JavaFile
import com.squareup.javapoet.TypeSpec
import javax.lang.model.element.Modifier

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'ru.dmitriymx.bukkit'
version '1.0'
jar.archiveBaseName.set(project.name)

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    annotationProcessor('org.spigotmc:plugin-annotations:1.2.2-SNAPSHOT')
    compileOnly('org.spigotmc:plugin-annotations:1.2.2-SNAPSHOT')

    compileOnly('org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT') {
        exclude(module: 'gson')
        exclude(module: 'guava')
        exclude(module: 'commons-lang')
        exclude(module: 'bungeecord-chat')
    }
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('')
}

def genSrcDir = "${project.buildDir}/generated/sources/plugininfo/java/main"
sourceSets.main.java {
    srcDirs += genSrcDir
}
idea.module {
    generatedSourceDirs += file(genSrcDir)
}

def static buildField(String name, String value) {
    return FieldSpec.builder(String.class, name)
        .addModifiers(Modifier.PUBLIC, Modifier.STATIC, Modifier.FINAL)
        .initializer('$S', value)
        .build()
}

def generatePluginInfoClass(Project project, String genSrcDir) {
    def rootPackageDir = 'ru/dmitriymx/bukkit/zerodurability'
    file("${genSrcDir}/${rootPackageDir}").mkdirs()

    JavaFile.builder("${rootPackageDir.replaceAll('/', '.')}", TypeSpec.classBuilder("PluginInfo")
        .addModifiers(Modifier.PUBLIC, Modifier.FINAL)
        .addField(buildField('NAME', "${project.name}"))
        .addField(buildField('VERSION', "${project.version.toString()}"))
        .addField(buildField('AUTHOR', 'DmitriyMX'))
        .addField(buildField('WEBSITE', 'https://gitlab.com/DmitriyMX/zerodurability'))
        .build())
        .build().writeTo(file(genSrcDir))
}
generatePluginInfoClass(project, genSrcDir)

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            url 'https://gitlab.com/api/v4/projects/34143427/packages/maven'
            credentials(HttpHeaderCredentials) {
                name = 'Job-Token'
                value = System.getenv('CI_JOB_TOKEN')
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
