//file:noinspection GroovyAssignabilityCheck
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

group 'ru.dmitriymx.bukkit'
version '1.0'
jar.archiveBaseName.set(project.name)

compileJava {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    annotationProcessor('org.spigotmc:plugin-annotations:1.2.2-SNAPSHOT')
    compileOnly('org.spigotmc:plugin-annotations:1.2.2-SNAPSHOT')

    compileOnly('org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT') {
        exclude(module: 'gson')
        exclude(module: 'guava')
        exclude(module: 'commons-lang')
        exclude(module: 'bungeecord-chat')
    }
}

processResources {
    filteringCharset 'UTF-8'
    filter(ReplaceTokens, tokens: [BUKKIT_PLUGIN_VERSION: project.version.toString()])
}


shadowJar {
    archiveBaseName.set(project.name)
    archiveVersion.set(project.version.toString())
    archiveClassifier.set('')
}

def genSrcDir = "${project.buildDir}/generated/sources/plugininfo/java/main"
sourceSets.main.java {
    srcDirs += genSrcDir
}
idea.module {
    generatedSourceDirs += file(genSrcDir)
}

def generatePluginInfoClass(Project project, String genSrcDir) {
    def rootPackageDir = 'ru/dmitriymx/bukkit/zerodurability'
    file("${genSrcDir}/${rootPackageDir}").mkdirs()
    //noinspection GrUnresolvedAccess
    file("${genSrcDir}/${rootPackageDir}/PluginInfo.java").text = """package ${rootPackageDir.replaceAll('/', '.')};

public final class PluginInfo {
    public static final String NAME = "${project.name}";
    public static final String VERSION = "${project.version.toString()}";
    public static final String AUTHOR = "DmitriyMX";
    public static final String WEBSITE = "https://gitlab.com/DmitriyMX/zerodurability";

    private PluginInfo() {}
}
"""
}
generatePluginInfoClass(project, genSrcDir)

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            url 'https://gitlab.com/api/v4/projects/34143427/packages/maven'
            credentials(HttpHeaderCredentials) {
                name = 'Job-Token'
                value = System.getenv('CI_JOB_TOKEN')
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}
